# Assembly: Loops; functions
_COSC 208, Introduction to Computer Systems, 2023-10-25_

## Announcements
* Midterm Exam 2: Mon, Oct 30 6:30pm-8:30pm
* Project 3 due Thurs, Nov 9

## Outline
* Warm-up
* Mapping C loops to assembly
* Functions

## Warm-up

* Q1: _The following C code was compiled into assembly (using `gcc`). For each line of assembly, indicate which original line of C code the assembly instruction was derived from._ 


```c
/* 1*/  int adjust(int value) {
/* 2*/      if (value > 0 && value < 10) {
/* 3*/          value -= 10;
/* 4*/      } else {
/* 5*/          value += 10;
/* 6*/      }
/* 7*/      return value;
/* 8*/  }
```

```
0000000000000754 <adjust>:
 754:   d10043ff    sub sp, sp, #0x10         // 1
 758:   b9000fe0    str w0, [sp, #12]         // 1
 75c:   b9400fe0    ldr w0, [sp, #12]         // 2
 760:   7100001f    cmp w0, #0x0              // 2
 764:   5400010d    b.le    784 <adjust+0x30> // 2
 768:   b9400fe0    ldr w0, [sp, #12]         // 2
 76c:   7100241f    cmp w0, #0xa              // 2
 770:   540000ac    b.ge    784 <adjust+0x30> // 2
 774:   b9400fe0    ldr w0, [sp, #12]         // 3
 778:   51002800    sub w0, w0, #0xa          // 3
 77c:   b9000fe0    str w0, [sp, #12]         // 3
 780:   14000004    b   790 <adjust+0x3c>     // 4
 784:   b9400fe0    ldr w0, [sp, #12]         // 5
 788:   11002800    add w0, w0, #0xa          // 5
 78c:   b9000fe0    str w0, [sp, #12]         // 5
 790:   b9400fe0    ldr w0, [sp, #12]         // 7
 794:   910043ff    add sp, sp, #0x10         // 7
 798:   d65f03c0    ret                       // 7
```

## Mapping C loops to assembly

* Example mapping while loops to assembly


```c
/* 1*/   int pow2(int n) {
/* 2*/       int result = 1;
/* 3*/       while (n > 0) {
/* 4*/           result = result * 2;
/* 5*/           n = n - 1;
/* 6*/       }
/* 7*/       return result;
/* 8*/   }
```

    2^10 = 1024


```
0000000000000754 <pow2>:
 754:   d10083ff        sub     sp, sp, #0x20    // 1
 758:   b9000fe0        str     w0, [sp, #12]    // 1
 75c:   52800020        mov     w0, #0x1         // 2
 760:   b9001fe0        str     w0, [sp, #28]    // 2
 764:   14000007        b       780 <pow2+0x2c>  // 3
 768:   b9401fe0        ldr     w0, [sp, #28]    // 4
 76c:   531f7800        lsl     w0, w0, #1       // 4
 770:   b9001fe0        str     w0, [sp, #28]    // 4
 774:   b9400fe0        ldr     w0, [sp, #12]    // 5
 778:   51000400        sub     w0, w0, #0x1     // 5
 77c:   b9000fe0        str     w0, [sp, #12]    // 5
 780:   b9400fe0        ldr     w0, [sp, #12]    // 3
 784:   7100001f        cmp     w0, #0x0         // 3
 788:   54ffff0c        b.gt    768 <pow2+0x14>  // 3
 78c:   b9401fe0        ldr     w0, [sp, #28]    // 7
 790:   910083ff        add     sp, sp, #0x20    // 7
 794:   d65f03c0        ret                      // 7
```

* Goto form


```c
#include <stdio.h>
int pow2_goto(int n) {
    int result = 1;
    goto loop_check;
loop_top:
    result = result * 2;
    n = n - 1;
loop_check:
    if (n > 0)
        goto loop_top;
    return result;
}
int main() {
    printf("2^10 = %d\n", pow2_goto(10));
}
```

    2^10 = 1024


* For loop can always be expressed using a while loop
    * Assembly code generated for a for loop matches the assembly code generated for a while loop

## Practice with loops

* _The following C code was compiled into assembly (using `gcc`)._


```c
/* 1*/   int powi(int m, int n) {
/* 2*/       int result = 1;
/* 3*/       int i = 0;
/* 4*/       while (i < n) {
/* 5*/           result *= m;
/* 6*/           i++;
/* 7*/       }
/* 8*/       return result;
/* 9*/   }
```

* Q2: _Label each line of assembly code with the line number of the line of C code from which the assembly instruction was derived._

```
0000000000000714 <powi>:
 714:   d10083ff    sub sp, sp, #0x20   // 1
 718:   b9000fe0    str w0, [sp, #12]   // 1
 71c:   b9000be1    str w1, [sp, #8]    // 1
 720:   52800020    mov w0, #0x1        // 2
 724:   b9001be0    str w0, [sp, #24]   // 2
 728:   b9001fff    str wzr, [sp, #28]  // 3
 72c:   14000008    b   74c <powi+0x38> // 4
 730:   b9401be1    ldr w1, [sp, #24]   // 5
 734:   b9400fe0    ldr w0, [sp, #12]   // 5
 738:   1b007c20    mul w0, w1, w0      // 5
 73c:   b9001be0    str w0, [sp, #24]   // 5
 740:   b9401fe0    ldr w0, [sp, #28]   // 6
 744:   11000400    add w0, w0, #0x1    // 6
 748:   b9001fe0    str w0, [sp, #28]   // 6
 74c:   b9401fe1    ldr w1, [sp, #28]   // 4
 750:   b9400be0    ldr w0, [sp, #8]    // 4
 754:   6b00003f    cmp w1, w0          // 4
 758:   54fffecb    b.lt    730 <powi+0x1c> // 4
 75c:   b9401be0    ldr w0, [sp, #24]   // 8
 760:   910083ff    add sp, sp, #0x20   // 8
 764:   d65f03c0    ret                 // 8
```

Q3: _Write a function called `powi_goto` that behaves the same as `powi` but matches the structure of the assembly code that will be generated for `powi`. (Hint: you'll need two `goto` statements.)_


```c
int powi_goto(int m, int n) {
    int result = 1;
    int i = 0;
    goto loop_check;
loop_top:
    result *= m;
    i++;
loop_check:
    if (i < n)
        goto loop_top;
    return result;
}
// Testing
#include <assert.h>
int main() {
    assert(powi_goto(5,3) == 125);
}
```

## Function calls in assembly

* Calling conventions
    * _In which registers are parameters stored?_ — `x0`/`w0`, `x1`/`w1`, `x2`/`w2`, ...
    * _In which register is the return value stored?_ — `x0`/`w0`
    * Return address stored in `x30`
    * Caller's return address stored in callee's stack frame
* Use `bl` (branch & link) to make a function call
    * Store `pc+4` in `x30`
    * Update `pc` to specified code address
* Example


```c
/*1*/  int multiply(int a, int b) {
/*2*/      int c = a * b;
/*3*/      return c;
/*4*/  }
/*5*/  int volume(int x, int y, int z) {
/*6*/      int w = multiply(x, y);
/*7*/      w = multiply(w, z);
/*8*/      return w;
/*9*/  }
/*10*/  int main() {
/*11*/      int n = volume(1, 3, 5);
/*12*/      return n;
/*13*/  }
```

```
0000000000400544 <multiply>:
    400544:    d10043ff     sub    sp, sp, #0x10    // 1
    400548:    b9000fe0     str    w0, [sp, #12]    // 1
    40054c:    b9000be1     str    w1, [sp, #8]     // 1
    400550:    b9400fe8     ldr    w8, [sp, #12]    // 2
    400554:    b9400be9     ldr    w9, [sp, #8]     // 2
    400558:    1b097d08     mul    w8, w8, w9       // 2
    40055c:    b90007e8     str    w8, [sp, #4]     // 2
    400560:    b94007e0     ldr    w0, [sp, #4]     // 3
    400564:    910043ff     add    sp, sp, #0x10    // 3
    400568:    d65f03c0     ret                     // 3
```

```
000000000040056c <volume>:
    40056c:    d10083ff     sub    sp, sp, #0x20    // 5
    400570:    f9000bfe     str    x30, [sp, #16]   // 5
    400574:    b9000fe0     str    w0, [sp, #12]    // 5
    400578:    b9000be1     str    w1, [sp, #8]     // 5
    40057c:    b90007e2     str    w2, [sp, #4]     // 5
    400580:    b9400fe0     ldr    w0, [sp, #12]    // 6
    400584:    b9400be1     ldr    w1, [sp, #8]     // 6
    400588:    97ffffef     bl    400544 <multiply> // 6
    40058c:    b90003e0     str    w0, [sp]         // 6
    400590:    b94003e0     ldr    w0, [sp]         // 7
    400594:    b94007e1     ldr    w1, [sp, #4]     // 7
    400598:    97ffffeb     bl    400544 <multiply> // 7
    40059c:    b90003e0     str    w0, [sp]         // 7
    4005a0:    b94003e0     ldr    w0, [sp]         // 8
    4005a4:    f9400bfe     ldr    x30, [sp, #16]   // 8
    4005a8:    910083ff     add    sp, sp, #0x20    // 8
    4005ac:    d65f03c0     ret                     // 8
```

```
00000000004005b0 <main>:                               
    4005b0:    d10083ff     sub    sp, sp, #0x20       // 10
    4005b4:    f9000bfe     str    x30, [sp, #16]      // 10
    4005b8:    52800020     mov    w0, #0x1            // 11
    4005bc:    52800061     mov    w1, #0x3            // 11
    4005c0:    528000a2     mov    w2, #0x5            // 11
    4005c4:    97ffffe9     bl     40056c <volume>     // 11
    4005c8:    b9000be0     str    w0, [sp, #8]        // 11
    4005cc:    b9400be0     ldr    w0, [sp, #8]        // 12
    4005d0:    f9400bfe     ldr    x30, [sp, #16]      // 12
    4005d4:    910083ff     add    sp, sp, #0x20       // 12
    4005d8:    d65f03c0     ret                        // 12
```

* Assume the registers have the following initial values:
    * `pc` = `0x4005b0`
    * `sp` = `0xFF0`
    * `x30` = `0x4005dc`

## Extra practice

Q4: _Write a function called `adjust_goto` that behaves the same as `adjust` but matches the structure of the assembly code that is generated for `adjust`. (Hint: you'll need three `goto` statements.)_


```c
/* 1*/  int adjust(int value) {
/* 2*/      if (value > 0 && value < 10) {
/* 3*/          value -= 10;
/* 4*/      } else {
/* 5*/          value += 10;
/* 6*/      }
/* 7*/      return value;
/* 8*/  }
```


```c
int adjust_goto(int value) {
    if (value <= 0)
        goto else_body;
    if (value >= 10)
        goto else_body;
    value -= 10;
    goto after_if;
else_body:
    value += 10;
after_if:
    return value;
}
// Testing
#include <assert.h>
int main() {
    assert(adjust_goto(5) == -5);
}
```

* Q5: _The following C code was compiled into assembly (using `gcc`). For each line of assembly, indicate which original line of C code the assembly instruction was derived from._ 


```c
/* 1*/  int flip_elif(int bit) {
/* 2*/      int result;
/* 3*/      if (bit == 0) {
/* 4*/          result = 1 ;
/* 5*/      }
/* 6*/      else if (bit == 1) {
/* 7*/          result = 0;
/* 8*/      }
/* 9*/      else {
/*10*/          result = -1;
/*11*/      }
/*12*/      return result;
/*13*/  }
```

```
000000000000071c <flip_elif>:
    71c:    d10083ff     sub    sp, sp, #0x20           // 1
    720:    b9000fe0     str    w0, [sp, #12]           // 1
    724:    b9400fe0     ldr    w0, [sp, #12]           // 3
    728:    7100001f     cmp    w0, #0x0                // 3
    72c:    54000081     b.ne   73c <flip_elif+0x20>    // 3
    730:    52800020     mov    w0, #0x1                // 4
    734:    b9001fe0     str    w0, [sp, #28]           // 4
    738:    14000008     b      758 <flip_elif+0x3c>    // 5
    73c:    b9400fe0     ldr    w0, [sp, #12]           // 6
    740:    7100041f     cmp    w0, #0x1                // 6
    744:    54000061     b.ne   750 <flip_elif+0x34>    // 6
    748:    b9001fff     str    wzr, [sp, #28]          // 7
    74c:    14000003     b      758 <flip_elif+0x3c>    // 8
    750:    12800000     mov    w0, #0xffffffff         // 10
    754:    b9001fe0     str    w0, [sp, #28]           // 10
    758:    b9401fe0     ldr    w0, [sp, #28]           // 12
    75c:    910083ff     add    sp, sp, #0x20           // 12
    760:    d65f03c0     ret                            // 12
```

Q6: _Write a function called `flip_elif_goto` that behaves the same as `flip_elif` but matches the structure of the assembly code that is generated for `flip_elif`._


```c
int flip_elif_goto(int bit) {
    int result;
    if (bit != 0)
        goto ELIF;
    result = 1;
    goto END;
ELIF:
    if (bit != 1)
        goto ELS;
    result = 0;
    goto END;
ELS:
    result = -1;
END:
    return result;
}
// Testing
#include <assert.h>
int main() {
    assert(flip_elif_goto(0) == 1);
    assert(flip_elif_goto(1) == 0);
    assert(flip_elif_goto(2) == -1);
}
```
