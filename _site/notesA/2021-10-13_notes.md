# Assembly: Tracing assembly (continued); mapping C code to assembly code
_COSC 208, Introduction to Computer Systems, 2021-10-13_

## Announcements
* Project 2 Part A due Thursday, Oct 21 

## Outline
* Warm-up
* Practice tracing assembly code
* Mapping C code to assembly code
* Practice translating, mapping, and tracing assembly code

## Warm-up
* Q1: _Assume the registers currently hold the following values:_
    ```
    sp = 0xA980
    w/x0 = 0
    w/x1 = 1
    w/x2 = 2
    w/x3 = 3
    w/x4 = 4
    w/x5 = 5
    ```
    _Draw the contents of the stack after the following instructions have been executed:_
    ```
    sub sp, sp, #0x30
    str w0, [sp, #16]
    str x1, [sp]
    str w2, [sp, #20]
    str x3, [sp, #32]
    str w4, [sp, #28]
    str w5, [sp, #8]
    ```
    ```
            0  1  2  3  4  5  6  7  8          
     sp ->  +--+--+--+--+--+--+--+--+
    0xA950  |           1           |
            +--+--+--+--+--+--+--+--+
    0xA958  |     5     |           |
            +--+--+--+--+--+--+--+--+
    0xA960  |     0     |     2     |
            +--+--+--+--+--+--+--+--+
    0xA968  |           |     4     |
            +--+--+--+--+--+--+--+--+
    0xA970  |           3           |
            +--+--+--+--+--+--+--+--+
    0xA978  |                       |
            +--+--+--+--+--+--+--+--+
    0xA980  |                       |
            +--+--+--+--+--+--+--+--+
    ```

## Practice trace assembly code
* Assembly code for a C function with the prototype `int seasons_of_love()`:
    ```
    0000000000400584 <seasons_of_love>:
        400584:	d10083ff 	sub	sp, sp, #0x20   // sp = sp - 0x20
        400588:	52802da8 	mov	w8, #365        // w8 = 365 
        40058c:	52800309 	mov	w9, #24         // w9 = 24
        400590:	5280078a 	mov	w10, #60        // w10 = 60
        400594:	b9001fe8 	str	w8, [sp, #28]   // *(sp + 28) = w8
        400598:	b9001be9 	str	w9, [sp, #24]   // *(sp + 24) = w9
        40059c:	b90017ea 	str	w10, [sp, #20]  // *(sp + 20) = w10
        4005a0:	b9401fe8 	ldr	w8, [sp, #28]   // w8 = *(sp + 28)
        4005a4:	b9401be9 	ldr	w9, [sp, #24]   // w9 = *(sp + 24)
        4005a8:	1b097d08 	mul	w8, w8, w9      // w8 = w8 * w9
        4005ac:	b90013e8 	str	w8, [sp, #16]   // *(sp + 16) = w8
        4005b0:	b94013e8 	ldr	w8, [sp, #16]   // w8 = *(sp + 16)
        4005b4:	b94017e9 	ldr	w9, [sp, #20]   // w9 = *(sp + 20)
        4005b8:	1b097d08 	mul	w8, w8, w9      // w8 = w8 * w9
        4005bc:	b9000fe8 	str	w8, [sp, #12]   // *(sp + 12) = w8
        4005c0:	b9400fe0 	ldr	w0, [sp, #12]   // w0 = *(sp + 12)
        4005c4:	910083ff 	add	sp, sp, #0x20   // sp = sp + 0x20
        4005c8:	d65f03c0 	ret	                // return
    ```
* Contents of the stack and registers just prior to the execution of the second-to-last instruction, assuming the initial value of `sp` is `0x7FE0`:
    ```
            0  1  2  3  4  5  6  7  8          
     sp ->  +--+--+--+--+--+--+--+--+
    0x7FC0  |           |           |
            +--+--+--+--+--+--+--+--+
    0x7FC8  |           |   525600  |
            +--+--+--+--+--+--+--+--+
    0x7FD0  |    8760   |     60    |
            +--+--+--+--+--+--+--+--+
    0x7FD8  |     24    |    365    |
            +--+--+--+--+--+--+--+--+
    ```
    ```
    sp = 0x7FC0
    w0 = 525600
    w8 = 525600
    w9 = 60 
    w10 = 60
    ```

## Mapping C code to assembly code
* Mathematical operation pattern
    * load (ldr) first operand from the stack into a register
    * load (ldr) second operand from the stack into a register
    * compute
    * store (str) resulting value from a register onto the stack
* Local variable initialization pattern
    * move (mov) constant value into register
    * store (str) register value onto the stack
* Function begin pattern
    * create stack frame by decreasing (sub) stack pointer
    * store (str) parameter values in registers w/x0, w/x1, etc. onto the stack
* Function return/end pattern
    * load (ldr) return value from the stack into the w/x0 register
    * destroy stack frame by increasing (add) stack pointer
    * return (ret) to caller
* Example:
    ```C
    1   #include <stdio.h>
    2   int seasons_of_love() {
    3       int dpy = 365;
    4       int hpd = 24;
    5       int mph = 60;
    6       int h = dpy * hpd;
    7       int m = h * mph;
    8       return m;
    9   }
    10
    11  int main() {
    12      int minutes = seasons_of_love();
    13      printf("%d minutes\n", minutes);
    14      printf("%d moments so dear\n", minutes - 600);
    15      printf("%d minutes\n", minutes);
    16      printf("How do you measure? Measure a year?\n");
    17  }
    ```
    ```
    0000000000400584 <seasons_of_love>:
        400584:	d10083ff 	sub	sp, sp, #0x20   // Line 2
        400588:	52802da8 	mov	w8, #365        // Line 3
        40058c:	52800309 	mov	w9, #24         // Line 4
        400590:	5280078a 	mov	w10, #60        // Line 5
        400594:	b9001fe8 	str	w8, [sp, #28]   // Line 3
        400598:	b9001be9 	str	w9, [sp, #24]   // Line 4
        40059c:	b90017ea 	str	w10, [sp, #20]  // Line 5
        4005a0:	b9401fe8 	ldr	w8, [sp, #28]   // Line 6
        4005a4:	b9401be9 	ldr	w9, [sp, #24]   //   |
        4005a8:	1b097d08 	mul	w8, w8, w9      //   |
        4005ac:	b90013e8 	str	w8, [sp, #16]   //   V
        4005b0:	b94013e8 	ldr	w8, [sp, #16]   // Line 7
        4005b4:	b94017e9 	ldr	w9, [sp, #20]   //   |
        4005b8:	1b097d08 	mul	w8, w8, w9      //   |
        4005bc:	b9000fe8 	str	w8, [sp, #12]   //   V
        4005c0:	b9400fe0 	ldr	w0, [sp, #12]   // Line 8
        4005c4:	910083ff 	add	sp, sp, #0x20   // Line 9
        4005c8:	d65f03c0 	ret	                //   V
    ```

## Practice translating, mapping, and tracing assembly code
* Q2: _The following C code was compiled into assembly._
    ```C
    1  int divide(int numerator, int denominator) {
    2      int result = -1;
    3      result = numerator / denominator;
    4      return result;
    5  }
    ```
    _For each line of assembly: (a) write the low-level C code equivalent, treating registers as if they were variable names; and (b) indicate which original line of C code (above) the assembly instruction was derived from._
    ```
    0000000000400544 <divide>:
        400544:	d10043ff 	sub	sp, sp, #0x10       // sp = sp - 0x10   // Line 1
        400548:	12800008 	mov	w8, #0xffffffff     // w8 = 0xffffffff  // Line 2
        40054c:	b9000fe0 	str	w0, [sp, #12]       // *(sp + 12) = w0  // Line 1
        400550:	b9000be1 	str	w1, [sp, #8]        // *(sp + 8) = w1   //   V
        400554:	b90007e8 	str	w8, [sp, #4]        // *(sp + 4) = w8   // Line 2
        400558:	b9400fe8 	ldr	w8, [sp, #12]       // w8 = *(sp + 12)  // Line 3
        40055c:	b9400be9 	ldr	w9, [sp, #8]        // w9 = *(sp + 8)   //   |
        400560:	1ac90d08 	sdiv	w8, w8, w9          // w8 = w8 / w9     //   |
        400564:	b90007e8 	str	w8, [sp, #4]        // *(sp + 4) = w8   //   V
        400568:	b94007e0 	ldr	w0, [sp, #4]        // w0 = *(sp + 4)   // Line 4
        40056c:	910043ff 	add	sp, sp, #0x10       // sp = sp + 0x10   // Line 5
        400570:	d65f03c0 	ret	                    // return           //   V
    ```
* Q3: _Assume the registers initially hold the following values:_
    ```
    sp = 0xFD0
    w/x0 = 100
    w/x1 = 5
    ```
    _Draw the contents of the stack and registers just prior to the execution of the second-to-last instruction (add sp, sp, #0x10) above._
    ```
            0  1  2  3  4  5  6  7  8          
     sp ->  +--+--+--+--+--+--+--+--+
     0xFC0  |           |     20    |
            +--+--+--+--+--+--+--+--+
     0xFC8  |     5     |    100    |
            +--+--+--+--+--+--+--+--+
     0xFD0  |  0x2238   |   0x3c    |
            +--+--+--+--+--+--+--+--+
    
    sp = 0xFC0
    w/x0 = 20
    w/x1 = 5
    w/x8 = 20
    w/x9 = 5
    ```
* _Why is `#0xffffffff` being stored in `w8`?_ — this is the two's complement representation of -1
* _When might this function cause an error?_ — when denominator is 0
* _How would you modify the C code to avoid an error?_
    ```C
    1  int divide_safe(int numerator, int denominator) {
    2      int result = -1;
    3      if (denominator != 0) {
    4          result = numerator / denominator;
    5      }
    6      return result;
    7  }
    ```