# Assembly: load/store operations; arithmetic and bitwise operations; translating assembly code to low-level C code
_COSC 208, Introduction to Computer Systems, 2022-10-14_

## Announcements
* Programming project 3 due Thursday @ 11pm

## Outline
* Warm-up: schedule change poll
* Practice mapping assembly code to C code
* Load/store operations 
* Arithmetic and bitwise operations
* Translating assembly code to low-level C code

## Warm-up: schedule change poll
https://pollev.com/aarongj

## Practice mapping assembly code to C code
* Q1: _The following C code was compiled into assembly._
    ```C
    1   #include <stdio.h>
    2   int years_to_double(int rate) {
    3       int ruleof72 = 72;
    4       int years = ruleof72 / rate;
    5       return years;
    6   }
    7   int main() {
    8       int r = 10;
    9       int y = years_to_double(r);
    10      printf("With an interest rate of %d%% it will take ~%d 
                years to double your money\n", r, y);
    11  }
    ```
    _For each line of assembly, indicate which original line of C code (above) the assembly instruction was derived from._
    ```
    000000000000076c <years_to_double>:
        76c:	d10083ff 	sub	sp, sp, #0x20   // 2
        770:	b9000fe0 	str	w0, [sp, #12]   // 2
        774:	52800900 	mov	w0, #0x48       // 3
        778:	b9001be0 	str	w0, [sp, #24]   // 3
        77c:	b9401be1 	ldr	w1, [sp, #24]   // 4
        780:	b9400fe0 	ldr	w0, [sp, #12]   // 4
        784:	1ac00c20 	sdiv	w0, w1, w0  // 4
        788:	b9001fe0 	str	w0, [sp, #28]   // 4
        78c:	b9401fe0 	ldr	w0, [sp, #28]   // 5
        790:	910083ff 	add	sp, sp, #0x20   // 5
        794:	d65f03c0 	ret                 // 5
    ```

## Load/store operations
* Load (`ldr`) — copies a value from a specific location in main memory into a specific register on the CPU
    * Can copy either 64-bits or 32-bits at a time, depending on whether the instruction specifies `x` or `w` register, respectively
    * Similar to dereferencing a pointer and storing the value in a local variable
        * For example, if we treat registers as if they were variable names, `ldr x0, [x1]` is similar to `x0 = *x1`, where `x0` has type `long` and `x1` has type `long *`
* Store (`str`) — copies a value from a specific register on the CPU into a specific location in main memory
    * Again, can copy 64-bits or 32-bits at a time, depending on whether the instruction specifies `x` or `w` register, respectively
    * Similar to storing the value in a local variable at a memory location pointed-to by a pointer
        * For example, if we treat registers as if they were variable names, `str x0, [x1]` is similar to `*x1 = x0`, where `x0` has type `long` and `x1` has type `long *`
* _Write the C code equivalent for each line of assembly, treating registers as if they were variable names._
    * Q2: `ldr x0, [sp]`
        ```C
        x0 = *sp
        ```
    * Q3: `str w0, [sp]`
        ```C
        *sp = w0
        ```
    * Q4: `ldr x1, [sp,#12]`
        ```C
        x1 = *(sp + 12)
        ```
    * Q5: `str x2, [x3,#0x10]`
        ```C
        *(x3 + 0x10) = x2
        ```

## Arithmetic and logical operations
* (Most) arithmetic and bitwise operations take three operands
    * Register into which to store the result of the operation
    * Register containing the first operand
    * Constant value or register containing the second operand
*_Write the C code equivalent for each line of assembly, treating registers as if they were variable names._
    * Q6: `lsl w0, w1, w2`
        ```C
        w0 = w1 << w2
        ```
    * Q7: `and w3, w4, #20`
        ```C
        w3 = w4 & 20
        ```
    * Q8: `mul w5, w6, #0x11`
        ```C
        w5 = w6 * 0x11
        ```
    * Q9: `sdiv w7, w8, w9`
        ```C
        w7 = w8 / w9
        ```

## Translating assembly code to low-level C code
The following C program (`operands.c`) has been compiled into assembly:
```C
int operandsA(int a) {
    return a;
}

long operandsB(long b) {
    return b;
}

int operandsC(int *c) {
    return *c;
}

long operandsD(long *d) {
    return *d;
}

int main() {
    operandsA(5);
    operandsB(5);
    int x = 5;
    operandsC(&x);
    long y = 5;
    operandsD(&y);
}
```
* Q10: _Write the C code equivalent for each line of assembly, treating registers as if they were variable names. The assembly code for the `operandsA` function has already been translated into C code._
    ```
    00000000000007ec <operandsA>:
        7ec:	d10043ff 	sub	sp, sp, #0x10   // sp = sp - 0x10
        7f0:	b9000fe0 	str	w0, [sp, #12]   // *(sp + 12) = w0
        7f4:	b9400fe0 	ldr	w0, [sp, #12]   // w0 = *(sp + 12)
        7f8:	910043ff 	add	sp, sp, #0x10   // sp = sp + 0x10
        7fc:	d65f03c0 	ret	                // return

    0000000000000800 <operandsB>:
        800:	d10043ff 	sub	sp, sp, #0x10   // sp = sp - 0x10
        804:	f90007e0 	str	x0, [sp, #8]    // *(sp + 8) = w0
        808:	f94007e0 	ldr	x0, [sp, #8]    // x0 = *(sp + 8)
        80c:	910043ff 	add	sp, sp, #0x10   // sp = sp + 0x10
        810:	d65f03c0 	ret	                // return

    0000000000000814 <operandsC>:
        814:	d10043ff 	sub	sp, sp, #0x10   // sp = sp - 0x10
        818:	f90007e0 	str	x0, [sp, #8]    // *(sp + 8) = x0
        81c:	f94007e0 	ldr	x0, [sp, #8]    // x0 = *(sp + 8)
        820:	b9400000 	ldr	w0, [x0]        // w0 = *x0
        824:	910043ff 	add	sp, sp, #0x10   // sp = sp + 0x10
        828:	d65f03c0 	ret	                // return

    000000000000082c <operandsD>:
        82c:	d10043ff 	sub	sp, sp, #0x10   // sp = sp - 0x10
        830:	f90007e0 	str	x0, [sp, #8]    // *(sp + 8) = x0
        834:	f94007e0 	ldr	x0, [sp, #8]    // x0 = *(sp + 8)
        838:	f9400000 	ldr	x0, [x0]        // x0 = *x0
        83c:	910043ff 	add	sp, sp, #0x10   // sp = sp + 0x10
        840:	d65f03c0 	ret	                // return
    ```
* Q11: _How does the assembly code for `operandsA` and `operandsB` differ? Why?_
    * `operandsA` takes and returns an int, which is 32-bits, whereas `operandsB` takes and returns a long, which is 64-bits, so:
        * `operandsA` uses `w0` while `operandsB` uses `x0`
        * `operandsA` stores the parameter at `sp + 12` while `operandsB` stores the parameter at `sp + 8`
* Q12: _How does the assembly code for `operandsB` and `operandsD` differ? Why?_
    * `operandsB` takes and returns a long, whereas `operandsD` takes a pointer to a long and returns a long, so:
        * `operandsD` must deference the pointer (`ldr x0, [x0]`) before returning 
* Q13: _How does the assembly code for `operandsC` and `operandsD` differ? Why?_
    * `operandsC` takes a pointer to an int and returns and int, whereas `operandsD` takes a pointer to a long and returns a long
    * both take a memory address (a 64-bit value), which is initially in `x0` and stored at `sp + 8`
    * the dereference of the pointer is a 32-bit value in `operandsC` and a 64-bit value in `operandsD`, so the value is loaded into `w0` in `operandsC` and `x0` in `operandsD`

## Extra practice
_Write the C code equivalent for each line of assembly, treating registers as if they were variable names._
* Q14: 
    ```
    00000000000007ac <seasons_of_love>:
        7ac:   d10083ff        sub     sp, sp, #0x20    // sp = sp + 0x20
        7b0:   52802da0        mov     w0, #0x16d       // w0 = 0x16d
        7b4:   b9000fe0        str     w0, [sp, #12]    // *(sp+12) = w0
        7b8:   52800300        mov     w0, #0x18        // w0 = 0x18 
        7bc:   b90013e0        str     w0, [sp, #16]    // *(sp+16) = w0
        7c0:   52800780        mov     w0, #0x3c        // w0 = 0x3c
        7c4:   b90017e0        str     w0, [sp, #20]    // *(sp+20) = w0
        7c8:   b9400fe1        ldr     w1, [sp, #12]    // w1 = *(sp+12)
        7cc:   b94013e0        ldr     w0, [sp, #16]    // w0 = *(sp+16)
        7d0:   1b007c20        mul     w0, w1, w0       // w0 = w1 * w0
        7d4:   b9001be0        str     w0, [sp, #24]    // *(sp+24) = w0
        7d8:   b9401be1        ldr     w1, [sp, #24]    // w1 = *(sp+24)
        7dc:   b94017e0        ldr     w0, [sp, #20]    // w0 = *(sp+20)
        7e0:   1b007c20        mul     w0, w1, w0       // w0 = w1 * w0
        7e4:   b9001fe0        str     w0, [sp, #28]    // *(sp+28) = w0
        7e8:   b9401fe0        ldr     w0, [sp, #28]    // w0 = *(sp+28)
        7ec:   910083ff        add     sp, sp, #0x20    // sp = sp + 0x20
        7f0:   d65f03c0        ret                      // return
    ```
* Q15: 
    ```
    000000000000076c <years_to_double>:
        76c:	d10083ff 	sub	sp, sp, #0x20   // sp = sp + 0x20
        770:	b9000fe0 	str	w0, [sp, #12]   // *(sp+12) = w0
        774:	52800900 	mov	w0, #0x48       // w0 = 0x48
        778:	b9001be0 	str	w0, [sp, #24]   // *(sp+24) = w0
        77c:	b9401be1 	ldr	w1, [sp, #24]   // w1 = *(sp+24)
        780:	b9400fe0 	ldr	w0, [sp, #12]   // w0 = *(sp+12)
        784:	1ac00c20 	sdiv	w0, w1, w0  // w0 = w1 / w0
        788:	b9001fe0 	str	w0, [sp, #28]   // *(sp+28) = w0
        78c:	b9401fe0 	ldr	w0, [sp, #28]   // w0 = *(sp+28)
        790:	910083ff 	add	sp, sp, #0x20   // sp = sp + 0x20
        794:	d65f03c0 	ret                 // return
    ```

<!--
## Warm-up
* Q0: _Write the C code equivalent for each line of assembly, treating registers as if they were variable names._
    ```
    000000000000083c <deref>:
            83c:   d10083ff        sub     sp, sp, #0x20    // sp = sp - 0x20
            840:   f90007e0        str     x0, [sp, #8]     // *(sp + 8) = x0
            844:   f94007e0        ldr     x0, [sp, #8]     // x0 = *(sp + 8)
            848:   f9400000        ldr     x0, [x0]         // x0 = *x0
            84c:   f9000fe0        str     x0, [sp, #24]    // *(sp + 24) = w0
            850:   f9400fe0        ldr     x0, [sp, #24]    // w0 = *(sp + 24)
            854:   910083ff        add     sp, sp, #0x20    // sp = sp + 0x20
            858:   d65f03c0        ret                      // return
    ```

## Practice tracing assembly code
* Q6: _The following is the assembly code for a C function with the following prototype: `int seasons_of_love()`. Write the C code equivalent for each line of assembly, treating registers as if they were variable names._
    ```
    0000000000400584 <seasons_of_love>:
        400584:	d10083ff 	sub	sp, sp, #0x20   // sp = sp - 0x20
        400588:	52802da8 	mov	w8, #0x16d      // w8 = 0x16 
        40058c:	52800309 	mov	w9, #0x18       // w9 = 0x18
        400590:	5280078a 	mov	w10, #0x3c      // w10 = 0x3c
        400594:	b9001fe8 	str	w8, [sp, #28]   // *(sp + 28) = w8
        400598:	b9001be9 	str	w9, [sp, #24]   // *(sp + 24) = w9
        40059c:	b90017ea 	str	w10, [sp, #20]  // *(sp + 20) = w10
        4005a0:	b9401fe8 	ldr	w8, [sp, #28]   // w8 = *(sp + 28)
        4005a4:	b9401be9 	ldr	w9, [sp, #24]   // w9 = *(sp + 24)
        4005a8:	1b097d08 	mul	w8, w8, w9      // w8 = w8 * w9
        4005ac:	b90013e8 	str	w8, [sp, #16]   // *(sp + 16) = w8
        4005b0:	b94013e8 	ldr	w8, [sp, #16]   // w8 = *(sp + 16)
        4005b4:	b94017e9 	ldr	w9, [sp, #20]   // w9 = *(sp + 20)
        4005b8:	1b097d08 	mul	w8, w8, w9      // w8 = w8 * w9
        4005bc:	b9000fe8 	str	w8, [sp, #12]   // *(sp + 12) = w8
        4005c0:	b9400fe0 	ldr	w0, [sp, #12]   // w0 = *(sp + 12)
        4005c4:	910083ff 	add	sp, sp, #0x20   // sp = sp + 0x20
        4005c8:	d65f03c0 	ret	                // return
    ```
* Q7: _Draw the contents of the stack and registers just prior to the execution of the second-to-last instruction. Assume the initial value of `sp` is `0x7FE0`._
    ```
            0  1  2  3  4  5  6  7  8          
     sp ->  +--+--+--+--+--+--+--+--+
    0x7FC0  |           |           |
            +--+--+--+--+--+--+--+--+
    0x7FC8  |           |  0x80520  |
            +--+--+--+--+--+--+--+--+
    0x7FD0  |  0x2238   |   0x3c    |
            +--+--+--+--+--+--+--+--+
    0x7FD8  |    0x18   |   0x16d   |
            +--+--+--+--+--+--+--+--+
    ```
    ```
    sp = 0x7FC0
    w0 = 0x80520
    w8 = 0x80520
    w9 = 0x3c
    w10 = 0x3c
    ```
-->