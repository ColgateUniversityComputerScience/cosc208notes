# Assembly: Tracing assembly code
_COSC 208, Introduction to Computer Systems, 2022-03-21_

## Announcements
* Project 2 due Thursday, March 31 

## Outline
* Warm-up
* Tracing assembly code
* Practice mapping, translating, and tracing assembly code

## Warm-up
* Q1: _The following C code was compiled into assembly (using `clang`)._
    ```C
    1  int sum(int a, int *b) {
    2     int c = *b;
    3     int d = a + c;
    4     return d;
    5  }
    ```
    _For each line of assembly: (1) indicate which original line of C code (above) the assembly instruction was derived from; and (2) write the low-level C code equivalent of the assembly instruction, treating registers as if they were variable names._
    ```
    0000000000400584 <sum>:                            // Line     Low-level C
        400584:    d10083ff     sub    sp, sp, #0x20   // 1        sp = sp - 0x20
        400588:    b9001fe0     str    w0, [sp, #28]   // 1        *(sp+28) = w0
        40058c:    f9000be1     str    x1, [sp, #16]   // 1        *(sp+16) = x1
        400590:    f9400be8     ldr    x8, [sp, #16]   // 2        x8 = *(sp+16)
        400594:    b9400109     ldr    w9, [x8]        // 2        w9 = *x8
        400598:    b9000fe9     str    w9, [sp, #12]   // 2        *(sp+12) = w9
        40059c:    b9401fe9     ldr    w9, [sp, #28]   // 3        w9 = *(sp+28)
        4005a0:    b9400fea     ldr    w10, [sp, #12]  // 3        w10 = *(sp+12)
        4005a4:    0b0a0129     add    w9, w9, w10     // 3        w9 = w9 + w10
        4005a8:    b9000be9     str    w9, [sp, #8]    // 3        *(sp+8) = w9
        4005ac:    b9400be0     ldr    w0, [sp, #8]    // 4        w0 = *(sp+8)
        4005b0:    910083ff     add    sp, sp, #0x20   // 4        sp = sp + 0x20
        4005b4:    d65f03c0     ret                    // 4        return
    ```

## Tracing assembly code
* Contents of the stack and registers when the function starts executing
    ```
    sp = 0xFB0
    w/x0 = 2
    w/x1 = 0xFC8 
    ```
    ```
     sp -> +--+--+--+--+--+--+--+--+
     0xFB0 |                       |
           +--+--+--+--+--+--+--+--+
     0xFB8 |                       |
           +--+--+--+--+--+--+--+--+
     0xFC0 |         0xFC8         |
           +--+--+--+--+--+--+--+--+
     0xFC8 |     3     |     2     |
           +--+--+--+--+--+--+--+--+
     0xFD0 |       0x400618        |
           +--+--+--+--+--+--+--+--+
     0xFD8 |                       |
           +--+--+--+--+--+--+--+--+
    ```
* Contents of the stack and registers *before* executing second-to-last assembly instruction (`add    sp, sp, #0x20`)
    ```
    sp = 0xF90
    w/x0 = 5
    w/x1 = 0xFC8
    w/x8 = 0xFC8
    w/x9 = 5
    w/x10 = 3
    ```
    ```
           0  1  2  3  4  5  6  7  8
     sp -> +--+--+--+--+--+--+--+--+
     0xF90 |                       |
           +--+--+--+--+--+--+--+--+
     0xF98 |     5     |     3     |
           +--+--+--+--+--+--+--+--+
     0xFA0 |         0xFC8         |
           +--+--+--+--+--+--+--+--+
     0xFA8 |           |     2     |
           +--+--+--+--+--+--+--+--+
     0xFB0 |                       |
           +--+--+--+--+--+--+--+--+
     0xFB8 |                       |
           +--+--+--+--+--+--+--+--+
     0xFC0 |         0xFC8         |
           +--+--+--+--+--+--+--+--+
     0xFC8 |     3     |     2     |
           +--+--+--+--+--+--+--+--+
     0xFD0 |       0x400618        |
           +--+--+--+--+--+--+--+--+
     0xFD8 |                       |
           +--+--+--+--+--+--+--+--+
    ```
* Q2: _Assume the following assembly code was generated (using `gcc`) for a C function with the prototype `int seasons_of_love()`:_
    ```
    00000000000007ac <seasons_of_love>:
        7ac:   d10083ff        sub     sp, sp, #0x20
        7b0:   52802da0        mov     w0, #0x16d   // 365
        7b4:   b9000fe0        str     w0, [sp, #12]
        7b8:   52800300        mov     w0, #0x18    // 24
        7bc:   b90013e0        str     w0, [sp, #16]
        7c0:   52800780        mov     w0, #0x3c    // 60
        7c4:   b90017e0        str     w0, [sp, #20]
        7c8:   b9400fe1        ldr     w1, [sp, #12]
        7cc:   b94013e0        ldr     w0, [sp, #16]
        7d0:   1b007c20        mul     w0, w1, w0
        7d4:   b9001be0        str     w0, [sp, #24]
        7d8:   b9401be1        ldr     w1, [sp, #24]
        7dc:   b94017e0        ldr     w0, [sp, #20]
        7e0:   1b007c20        mul     w0, w1, w0   
        7e4:   b9001fe0        str     w0, [sp, #28]
        7e8:   b9401fe0        ldr     w0, [sp, #28]
        7ec:   910083ff        add     sp, sp, #0x20
        7f0:   d65f03c0        ret
    ```                  
    _Draw the contents of the stack and registers just prior to the execution of the second-to-last instruction (`add sp, sp, #0x20`). Assume the initial value in the `sp` register is `0x7FE0`._
    ```
            0  1  2  3  4  5  6  7  8          
     sp ->  +--+--+--+--+--+--+--+--+
    0x7FC0  |           |           |
            +--+--+--+--+--+--+--+--+
    0x7FC8  |           |    365    |
            +--+--+--+--+--+--+--+--+
    0x7FD0  |    24     |     60    |
            +--+--+--+--+--+--+--+--+
    0x7FD8  |    8760   |   525600  |
            +--+--+--+--+--+--+--+--+
    ```
    ```
    sp = 0x7FC0
    w/x0 = 525600
    w/x1 = 8760
    ```

## Practice mapping, translating, and tracing assembly code
* Q3: _The following C code was compiled into assembly (using `gcc`)._
    ```C
    1  int divide(int numerator, int denominator) {
    2      int result = -1;
    3      result = numerator / denominator;
    4      return result;
    5  }
    ```
    _For each line of assembly: (1) indicate which original line of C code (above) the assembly instruction was derived from; and (2) write the low-level C code equivalent of the assembly instruction, treating registers as if they were variable names._
    ```
    000000000000076c <divide>:                      // Line     Low-level C
        76c:    d10083ff     sub    sp, sp, #0x20   // 1        sp = sp - 0x20
        770:    b9000fe0     str    w0, [sp, #12]   // 1        *(sp+12) = w0
        774:    b9000be1     str    w1, [sp, #8]    // 1        *(sp+8) = w1
        778:    12800000     mov    w0, #0xffffffff // 2        w0 = 0xffffffff
        77c:    b9001fe0     str    w0, [sp, #28]   // 2        *(sp+28) = w0
        780:    b9400fe1     ldr    w1, [sp, #12]   // 3        w1 = *(sp+12)
        784:    b9400be0     ldr    w0, [sp, #8]    // 3        w0 = *(sp+8)
        788:    1ac00c20     sdiv   w0, w1, w0      // 3        w0 = w1/w0
        78c:    b9001fe0     str    w0, [sp, #28]   // 3        *(sp+28) = w0
        790:    b9401fe0     ldr    w0, [sp, #28]   // 4        w0 = *(sp+28)
        794:    910083ff     add    sp, sp, #0x20   // 4        sp = sp + 0x20
        798:    d65f03c0     ret                    // 4        return
    ```
* Q4: _Assume the registers initially hold the following values:_
    ```
    sp = 0xFE0
    w/x0 = 100
    w/x1 = 5
    ```
    _Draw the contents of the stack and registers just prior to the execution of the second-to-last instruction (`add sp, sp, #0x10`) above._
    ```
            0  1  2  3  4  5  6  7  8          
     sp ->  +--+--+--+--+--+--+--+--+
     0xFC0  |                       |
            +--+--+--+--+--+--+--+--+
     0xFC8  |     5     |    100    |
            +--+--+--+--+--+--+--+--+
     0xFD0  |                       |
            +--+--+--+--+--+--+--+--+
     0xFD8  |           |     20    |
            +--+--+--+--+--+--+--+--+
    sp = 0xFC0
    w/x0 = 20
    w/x1 = 100
    ```
* _Why is `#0xffffffff` being stored in `w0`?_ — this is the two's complement representation of -1
* _When might this function cause an error?_ — when denominator is 0
* _How would you modify the C code to avoid an error?_
    ```C
    1  int divide_safe(int numerator, int denominator) {
    2      int result = -1;
    3      if (denominator != 0) {
    4          result = numerator / denominator;
    5      }
    6      return result;
    7  }
    ```

## Extra practice
* Q5: _Assume the following assembly code was generated (using `gcc`) for a C function with the prototype `int years_to_double(int rate)`:_
    ```
    000000000000076c <years_to_double>:
        76c:    d10083ff     sub    sp, sp, #0x20
        770:    b9000fe0     str    w0, [sp, #12]
        774:    52800900     mov    w0, #0x48   // 72
        778:    b9001be0     str    w0, [sp, #24]
        77c:    b9401be1     ldr    w1, [sp, #24]
        780:    b9400fe0     ldr    w0, [sp, #12]
        784:    1ac00c20     sdiv   w0, w1, w0
        788:    b9001fe0     str    w0, [sp, #28]
        78c:    b9401fe0     ldr    w0, [sp, #28]
        790:    910083ff     add    sp, sp, #0x20
        794:    d65f03c0     ret
    ```
    _Assume the registers initially hold the following values:_
    ```
    sp = 0x780
    w/x0 = 9
    ```
    _Draw the contents of the stack and registers just prior to the execution of the second-to-last instruction (`add sp, sp, #0x10`) above._
    ```
            0  1  2  3  4  5  6  7  8          
     sp ->  +--+--+--+--+--+--+--+--+
     0x760  |                       |
            +--+--+--+--+--+--+--+--+
     0x768  |     5     |     9     |
            +--+--+--+--+--+--+--+--+
     0x770  |                       |
            +--+--+--+--+--+--+--+--+
     0x778  |    72     |     8     |
            +--+--+--+--+--+--+--+--+
    sp = 0x760
    w/x0 = 8
    w/x1 = 72
    ```